channel zigbee 0;
channel wifi 0;
channel attackerbuffer 3;

enum {
	BeaconRequest, PanID, HubID, DeviceID, AssoPermit,
	UPnPMsearchRequest, CPIP, AttackerIP, ServerName, HubIP, 
	SearchLightRequest, RequestSuccess, ScanRequest, ScanResponse,
	IdentifyRequest, NetworkJoinRequest, NetworkJoinResponse, LinkNetworkJoinResponse, 
	JoinNearestDeviceRequest, LinkScanRequest, LinkScanResponse, 
	LinkIdentifyRequest,LinkNetworkJoinRequest,
	RequestLightResult, LightNo, LightName, GetInfoRequest, 
	Configs, Lights, Whitelist, DeleteLightRequest,
	AdminSuccess, DeleteUserIDRequest, LinkButtonTrue, 
	Controlcmd, EncryptedControlcmd, ACK, ControlCmdSuccess, 
	CP, HS, ZFE, SD, Nobody, CP_nonce, CP_hashed_nonce, 
	Attacker_nonce, Attacker_hashed_nonce
};

var CP_Connects = Nobody;
var ZFE_Connects = Nobody;
var SD_Connects = Nobody;
var HS_Connects = Nobody;

var HS_Whitelist = Nobody;
var LinkBridge = false;

var Network_HubID = Nobody;
var New_DeviceID = Nobody;
var Bridge_HubIP = Nobody;
var SD_Joined_Network = false;

SD_Proc() = SD_Discovery();
HS_Proc() = HS_Discovery();
ZFE_Proc() = ZFE_Discovery();
CP_Proc() = CP_Discovery();


//====================S1: Discovery Stage====================

SD_Discovery() = 
	SD_NetworkDiscovery() [] 
	SD_DeviceDiscovery();
ZFE_Discovery() = 
	ZFE_NetworkDiscovery() [] 
	ZFE_DeviceDiscovery();

SD_NetworkDiscovery() = 
	zigbee!BeaconRequest -> 
	zigbee?PanID.hub_id.AssoPermit -> 
	NetworkHubDiscovered{Network_HubID = hub_id} ->
	zigbee!PanID.DeviceID ->
	SD_JoinNetwork();
 
ZFE_NetworkDiscovery() = 
	zigbee?BeaconRequest -> 
	zigbee!PanID.HubID.AssoPermit -> 
	zigbee?PanID.device_id ->
	NewDeviceDiscovered{New_DeviceID = device_id} -> 
	ZFE_JoinNetwork();

SD_DeviceDiscovery() = 
	zigbee?BeaconRequest.hub_id -> 
	NetworkHubDiscovered{Network_HubID = hub_id} ->
	zigbee!PanID.DeviceID.AssoPermit ->
	SD_JoinDevice();

ZFE_DeviceDiscovery() = 
	zigbee!BeaconRequest.HubID -> 
	zigbee?PanID.device_id.AssoPermit ->
	NewDeviceDiscovered{New_DeviceID = device_id} ->
	ZFE_JoinDevice();

CP_Discovery() = 
	wifi!UPnPMsearchRequest.CPIP -> 
	wifi?CPIP.ServerName.hub_ip.hub_id ->
	ServerDiscovered{Bridge_HubIP = hub_ip} ->
	CP_Auth();
				
HS_Discovery() = 
	wifi?UPnPMsearchRequest.cp_ip ->
	wifi!cp_ip.ServerName.HubIP.HubID -> 
	HS_Auth();
				

//SystemStage1() = SD_Discovery() ||| ZFE_Discovery() ||| CP_Discovery() ||| HS_Discovery();



//====================S2: Authentication Stage====================

// TR 9-10
CP_Auth() = 
	newnonce.CP_nonce -> 
	wifi!Bridge_HubIP.CP_nonce ->  
	wifi?CPIP.hash_x ->
	CP_SendRequest(hash_x);

HS_Auth() = 
	wifi?HubIP.x ->  
	HS_AuthFromNonce(x);
	
HS_AuthFromNonce(x) = 
	if(x == CP_nonce){
		executeCommand{LinkBridge = true} -> 
		wifi!CPIP.CP_hashed_nonce -> 
		serverconnected{HS_Whitelist = CP_hashed_nonce} ->
		executeCommand{LinkBridge = false} -> 
		HS_ReceiveRequest()
	}
	else{
		executeCommand{LinkBridge = true} -> 
		wifi!AttackerIP.Attacker_hashed_nonce -> 
		serverconnected{HS_Whitelist = Attacker_hashed_nonce} ->
		executeCommand{LinkBridge = false} -> 
		HS_ReceiveRequest()
	};

// TR 11-17
CP_SendSearchLightRequest(hash_x) = 
	wifi!Bridge_HubIP.hash_x.SearchLightRequest -> 
	wifi?CPIP.RequestSuccess -> 
	CP_SendRequest(hash_x);
	
HS_ReceiveSearchLightRequest() = 
	wifi?HubIP.x.SearchLightRequest -> 
	HS_SearchLightResponse();

ZFE_JoinNetwork() = 
	zigbee!ScanRequest.PanID -> 
	zigbee?HubID.PanID.ScanResponse -> 
	zigbee!New_DeviceID.IdentifyRequest -> 
	zigbee!New_DeviceID.PanID.NetworkJoinRequest -> 
	zigbee?HubID.PanID.NetworkJoinResponse -> 
	HS_ReceiveRequest();

SD_JoinNetwork() = 
	zigbee?ScanRequest.PanID -> 
	zigbee!Network_HubID.PanID.ScanResponse -> 
	zigbee?DeviceID.IdentifyRequest -> 
	executeCommand.IdentifyRequest -> 
	zigbee?DeviceID.PanID.NetworkJoinRequest -> 
	zigbee!Network_HubID.PanID.NetworkJoinResponse -> 
	JoinToNetwork{SD_Joined_Network = true} ->
	SD_ReceiveRequest();



// TR 18-24
CP_SendJoinNearestDeviceRequest(hash_x) = 
	wifi!Bridge_HubIP.hash_x.JoinNearestDeviceRequest -> 
	wifi?CPIP.RequestSuccess->
	CP_SendRequest(hash_x);

HS_ReceiveJoinNearestDeviceRequest() = 
	wifi?HubIP.x.JoinNearestDeviceRequest -> 
	HS_JoinNearestDeviceResponse();
	
ZFE_JoinDevice() = 
	zigbee!LinkScanRequest.PanID -> 
	zigbee?HubID.PanID.LinkScanResponse -> 
	zigbee!New_DeviceID.LinkIdentifyRequest -> 
	zigbee!New_DeviceID.PanID.LinkNetworkJoinRequest -> 
	zigbee?HubID.PanID.LinkNetworkJoinResponse -> 
	HS_ReceiveRequest();
	
SD_JoinDevice() = 
	zigbee?LinkScanRequest.PanID -> 
	zigbee!Network_HubID.PanID.LinkScanResponse -> 
	zigbee?DeviceID.IdentifyRequest -> 
	executeCommand.LinkIdentifyRequest -> 
	zigbee?DeviceID.PanID.LinkNetworkJoinRequest -> 
	zigbee!Network_HubID.PanID.LinkNetworkJoinResponse -> 
	JoinToNetwork{SD_Joined_Network = true} ->
	SD_ReceiveRequest();
	
HS_SearchLightResponse() = 
	if(HS_Whitelist == CP_hashed_nonce){
		wifi!CPIP.RequestSuccess -> 
		ZFE_JoinNetwork()
	}
	else if(HS_Whitelist == Attacker_hashed_nonce){
		wifi!AttackerIP.RequestSuccess -> 
		ZFE_JoinNetwork()
	}
	else{
		HS_ReceiveRequest()
	};

HS_JoinNearestDeviceResponse() = 
	if(HS_Whitelist == CP_hashed_nonce){
		wifi!CPIP.RequestSuccess -> 
		ZFE_JoinDevice()
	}
	else if(HS_Whitelist == Attacker_hashed_nonce){
		wifi!AttackerIP.RequestSuccess -> 
		ZFE_JoinDevice()
	}
	else{
		HS_ReceiveRequest()
	};

CP_SendRequest(hash_x) = 
	CP_SendSearchLightRequest(hash_x) []
	CP_SendJoinNearestDeviceRequest(hash_x) []
	CP_RequestLightResult(hash_x) []
	CP_GetInfoRequest(hash_x);
	
HS_ReceiveRequest() = 
	HS_ReceiveSearchLightRequest() []
	HS_ReceiveJoinNearestDeviceRequest() []
	HS_RequestLightResult() []
	HS_GetInfoRequest();	
	
SD_ReceiveRequest() = 
	SD_JoinNetwork() []
	SD_JoinDevice() []
	SD_Controlcmd();
	
//SystemStage2() = CP_Auth() ||| HS_Auth() ||| SD_ReceiveRequest();



//====================S3: Control Stage====================


CP_SendAdminControlRequest(hash_x) = 
	CP_GetInfoRequest(hash_x) [] 
	CP_DeleteLightRequest(hash_x) [] 
	CP_DeleteUserIDRequest(hash_x) [] 
	CP_LinkButtonTrue(hash_x) []
	CP_SendRequest(hash_x);
	
HS_ReceiveAdminControlRequest() = 
	HS_GetInfoRequest() [] 
	HS_DeleteLightRequest() []
	HS_DeleteUserIDRequest() [] 
	HS_LinkButtonTrue() []
	HS_ReceiveRequest();
	
CP_SendControlRequest(hash_x) = 
	CP_RequestLightResult(hash_x) [] 
	CP_Controlcmd(hash_x) []
	CP_SendRequest(hash_x);
	
HS_ReceiveControlRequest() = 
	HS_RequestLightResult() [] 
	HS_Controlcmd() []
	HS_ReceiveRequest();
	

// TR 25-26
CP_RequestLightResult(hash_x) = 
	wifi!Bridge_HubIP.hash_x.RequestLightResult -> 
	wifi?CPIP.LightNo.LightName -> 
	CP_SendControlRequest(hash_x);
	
HS_RequestLightResult() = 
	wifi?HubIP.x.RequestLightResult -> 
	HS_LightResultResponse();


// TR 27-34
CP_GetInfoRequest(hash_x) = 
	wifi!Bridge_HubIP.hash_x.GetInfoRequest -> 
	wifi?CPIP.Configs.Lights.Whitelist -> 
	CP_SendAdminControlRequest(hash_x);
	
HS_GetInfoRequest() = 
	wifi?HubIP.x.GetInfoRequest -> 
	HS_GetInfoResponse();


CP_DeleteLightRequest(hash_x) = 
	wifi!Bridge_HubIP.hash_x.DeleteLightRequest -> 
	wifi?CPIP.AdminSuccess -> 
	CP_SendAdminControlRequest(hash_x);
					
HS_DeleteLightRequest() = 
	wifi?HubIP.x.DeleteLightRequest -> 
	HS_AdminSuccessResponse();
					
CP_DeleteUserIDRequest(hash_x) = 
	wifi!Bridge_HubIP.hash_x.DeleteUserIDRequest -> 
	wifi?CPIP.AdminSuccess -> 
	CP_SendAdminControlRequest(hash_x);

HS_DeleteUserIDRequest() = 
	wifi?HubIP.x.DeleteUserIDRequest -> 
	HS_AdminSuccessResponse();

CP_LinkButtonTrue(hash_x) = 
	wifi!Bridge_HubIP.hash_x.LinkButtonTrue -> 
	wifi?CPIP.AdminSuccess -> 
	CP_SendAdminControlRequest(hash_x);

HS_LinkButtonTrue() = 
	wifi?HubIP.x.LinkButtonTrue -> 
	executeCommand{LinkBridge = true} -> 
	HS_AdminSuccessResponse();


// TR 35-38
CP_Controlcmd(hash_x) = 
	wifi!Bridge_HubIP.hash_x.Controlcmd -> 
	wifi?CPIP.ControlCmdSuccess ->
	CP_SendRequest(hash_x);

HS_Controlcmd() = 
	wifi?HubIP.x.Controlcmd -> 
	HS_ControlSuccessResponse();
	

HS_LightResultResponse() = 
	if(HS_Whitelist == CP_hashed_nonce){
		wifi!CPIP.LightNo.LightName -> 
		HS_ReceiveControlRequest()
	}
	else if(HS_Whitelist == Attacker_hashed_nonce){
		wifi!AttackerIP.LightNo.LightName -> 
		HS_ReceiveControlRequest()
	}
	else{
		HS_ReceiveControlRequest()
	};
	

HS_GetInfoResponse() = 
	if(HS_Whitelist == CP_hashed_nonce){
		wifi!CPIP.LightNo.LightName -> 
		HS_ReceiveControlRequest()
	}
	else if(HS_Whitelist == Attacker_hashed_nonce){
		wifi!AttackerIP.LightNo.LightName -> 
		HS_ReceiveControlRequest()
	}
	else{
		HS_ReceiveControlRequest()
	};


HS_AdminSuccessResponse() = 
	if(HS_Whitelist == CP_hashed_nonce){
		wifi!CPIP.AdminSuccess -> 
		HS_ReceiveRequest()
	}
	else if(HS_Whitelist == Attacker_hashed_nonce){
		wifi!AttackerIP.AdminSuccess -> 
		HS_ReceiveRequest()
	}
	else{
		HS_ReceiveRequest()
	};
	
HS_ControlSuccessResponse() = 
	if(HS_Whitelist == CP_hashed_nonce){
		zigbee!New_DeviceID.PanID.EncryptedControlcmd -> 
		zigbee?HubID.ACK -> 
		wifi!CPIP.ControlCmdSuccess ->
		HS_ReceiveRequest()
	}
	else if(HS_Whitelist == Attacker_hashed_nonce){
		zigbee!New_DeviceID.PanID.EncryptedControlcmd -> 
		zigbee?HubIP.ACK -> 
		wifi!AttackerIP.ControlCmdSuccess ->
		HS_ReceiveRequest()
	}
	else{
		HS_ReceiveRequest()
	};


SD_Controlcmd() = 
	zigbee?DeviceID.PanID.EncryptedControlcmd -> 
	executeCommand.EncryptedControlcmd -> 
	zigbee!Network_HubID.ACK -> 
	SD_ReceiveRequest();

//SystemStage3() = CP_SendAdminControlRequest() ||| HS_ReceiveAdminControlRequest() ||| SD_Controlcmd();

//
//
//AttackerProc() = AttackerNegotiate() [] AttackerAsServer() [] AttackerRelay() [] AttackerReNegotiate();
//
//AttackerRelay() = attackerBuffer?x.y -> network!attacker_server_key.x.y -> AttackerProc()
//               [] network?y.z -> attackerBuffer!y.z -> AttackerProc() 
//               [] network?attacker_server_key.y -> network!y -> AttackerProc() 
//               [] network?y.z -> network!attacker_server_key.y.z -> AttackerProc();
//
//AttackerAsServer() = network?Client.Hello -> attackerBuffer!Client.Hello -> AttackerProc();
//
//AttackerAsCP_Discovery() = 
//	wifi!UPnPMsearchRequest -> 
//	wifi?AttackerIP.ServerName.HubIP.HubID -> 
//	CP_Auth();
//	attackerconnected{attacker_server_connected = true} ->
//	AttackerConnected();
//
//AttackerConnected() = network?attacker_server_key.HelloRequest -> AttackerProc();
//
//AttackerReNegotiate() = 
//   network!attacker_server_key.Attacker.Hello ->
//   network?attacker_server_key.ServerHello ->
//   network?attacker_server_key.ServerCertificate ->
//   network?attacker_server_key.ServerKeyExchange ->
//   network?attacker_server_key.ServerCertificateRequest ->
//   network?attacker_server_key.ServerHelloDone ->
//   network!attacker_server_key.Attacker.Certificate ->
//   network!attacker_server_key.Attacker.KeyExchange ->
//   network!attacker_server_key.Attacker.CertificateVerify ->
//   network!attacker_server_key.Attacker.ChangeCipherSpec ->
//   network!attacker_server_key.Attacker.Finished ->
//   network?attacker_server_key.ServerChangeCipherSpec ->
//   network?attacker_server_key.ServerFinished ->
//   AttackerConnected();
//
//SpecificAttacker = 
//   network?Client.Hello -> attackerBuffer!Client.Hello ->
//   network!Attacker.Hello ->
//   network?ServerHello ->
//   network?ServerCertificate ->
//   network?ServerKeyExchange ->
//   network?ServerCertificateRequest ->
//   network?ServerHelloDone ->
//   network!Attacker.Certificate ->
//   network!Attacker.KeyExchange ->
//   network!Attacker.CertificateVerify ->
//   network!Attacker.ChangeCipherSpec ->
//   network!Attacker.Finished ->
//   network?ServerChangeCipherSpec ->
//   network?ServerFinished ->
//   attackerconnected{attacker_server_connected = true} ->
//   network?attacker_server_key.HelloRequest ->
//   attackerBuffer?Client.Hello ->
//   network!attacker_server_key.Client.Hello -> 
//   SpecificRelay();
//
//SpecificRelay() = network?attacker_server_key.y -> network!y -> SpecificRelay()
//               [] network?x.z -> network!attacker_server_key.x.z -> SpecificRelay();
//               
               
               
//zigbeeSystem = SD_Proc() ||| Hub_Proc();
//wifiSystem = Hub_Proc()||| CP_Proc();
//
//#assert zigbeeSystem deadlockfree; 
//#assert wifiSystem deadlockfree; 				

system = SD_Proc() ||| HS_Proc() |||  ZFE_Proc()||| CP_Proc();

#assert system deadlockfree;